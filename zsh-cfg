#!/usr/bin/env zsh
cd $ZDOTDIR || exit 1

typeset -A files=(init .zshrc env .zshenv profile .zprofile)
help_message='Subcommands: clean bundle compile update'

function clean() {
    rm -f .*.zwc ${(k)files}/**/.bundle(N)
}

function bundle() {
    for plug in ${(k)files}/**/*.plug(N); do
        antibody bundle < $plug > $plug.bundle
        print -u2 "Created: $plug.bundle"
    done
}

function compile() {
    for dir file in ${(kv)files}; do
        cat $dir/**/*.(zsh|bundle)(N) | rg -v '^\s*(#|$)' > $file
        zcompile $file
        print -u2 "Created: $file"
    done
}

(($# == 0)) && echo $help_message
for command; do
    case $command in
        clean)    clean  ;;
        bundle)   bundle ;;
        compile)  compile ;;
        update)   antibody update ;;
        *)        echo $help_message ;;
    esac
done

unset files help_message
unfunction bundle compile
cd $OLDPWD
