#!/usr/bin/env zsh
cd ${ZDOTDIR:-$HOME} || exit 1

typeset -A files=(init .zshrc env .zshenv profile .zprofile)

function clean {
    rm -fv .*.zwc(N) ${~${(k)files}/%/'/**/*.bundle(N)'} >&2 2>/dev/null
}

function compile {
    awk_program='
    !/^\s*(#|$)/ {
        if (FILENAME ~ /\.zshx$/) {
            system(FILENAME);
            nextfile;
        }
        print;
    }'

    for dir file in ${(kv)files} ; do
        chmod +x $dir/**/*.zshx(Nr^x) 2>/dev/null
        awk $awk_program $dir/**/*.(zsh|zshx)(Nr) > $file
        zcompile -UR $file
        print -u2 "Created: $file"
    done
}

function update {
    antibody update >&2
    for script in ${~${(k)files}/%/'/**/update-script(Nr)'} ; do
        print -u2 "Sourcing: $script"
        source $script
    done
}

function print_help {
    echo 'Subcommands: clean compile update'
}

(( $# )) || print_help
for command ; do
    case $command in
    clean)   clean      ;;
    compile) compile    ;;
    update)  update     ;;
    *)       print_help ;;
    esac
done
