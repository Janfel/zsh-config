#!/usr/bin/env zsh
cd $ZDOTDIR || exit 1

typeset -A files=(init .zshrc env .zshenv profile .zprofile)
help_message='Subcommands: clean compile update'
awk_program='
!/^\s*(#|$)/ {
    if (FILENAME ~ /\.zshx$/) {
        system(FILENAME);
        nextfile;
    } else {
        print;
    }
}'

function clean() {
    rm -fv .*.zwc(N) ${~${(k)files}/%/'/**/*.bundle(N)'} >&2 2>/dev/null
}

function compile() {
    for dir file in ${(kv)files}; do
        chmod +x $dir/**/*.zshx(Nr^x) 2>/dev/null
        awk $awk_program $dir/**/*.(zsh|zshx|bundle)(Nr) > $file
        zcompile -UR $file
        print -u2 "Created: $file"
    done
}

function update() {
    antibody update >&2
    for script in ${~${(k)files}/%/'/**/update-script(Nr)'}; do
        print -u2 "Sourcing: $script"
        source $script
    done
}

(($# == 0)) && echo $help_message
for command; do
    case $command in
        clean)    clean  ;;
        compile)  compile ;;
        update)   update ;;
        *)        echo $help_message ;;
    esac
done
