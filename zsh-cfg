#!/usr/bin/env zsh
cd ${ZDOTDIR:-$HOME} || exit 1

local -A files=(init .zshrc env .zshenv profile .zprofile)
local help_message
read -rd '' help_message <<EOF
Usage: $0 <command>

Commands:
  clean        Remove the compiled .zwc files.
  compile      Build the Zsh config files by running the .zshx files.
  update       Update the plugins using antibody and run the update-script files.

EOF

function clean() {
	rm -fv .*.zwc(N) >&2 2>/dev/null
}

function compile() {
	export HOST=${HOST:-"$(hostname -s)"}
	for dir file in ${(kv)files}; do {
		chmod +x $dir/**/*.zshx(Nr^x) 2>/dev/null
		./compile.awk $dir/**/*.(zsh|zshx)(Nr) >$file
		zcompile -UR $file
		print -u2 "Created: $file"
	} & done
	wait
}

function update() { antibody update >&2 }

# Print help if no arguments were passed.
(( $# )) || printf '%s\n' $help_message

# Check for invalid commands.
for command; do
	if [[ $command != (clean|compile|update) ]]; then
		# Invalid command.
		printf '%s\n' $help_message
		exit 0;
	fi
done

for command; do
	case $command in
	(clean)   clean   ;;
	(compile) compile ;;
	(update)  update  ;;
	esac
done
