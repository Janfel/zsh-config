#!/usr/bin/env zsh
cd $ZDOTDIR || exit 1

typeset -A files=(init .zshrc env .zshenv profile .zprofile)
help_message='Subcommands: clean bundle compile update'
awk_program='
!/^\s*(#|$)/ {
    if (FILENAME ~ /\.zshx$/) {
        print FILENAME | "/bin/sh";
        nextfile;
    } else {
        print;
    }
}'

function clean() {
    rm -fv .*.zwc(N) ${~${(k)files}/%/'/**/*.bundle(N)'} >&2 2>/dev/null
}

function bundle() {
    for plug in ${~${(k)files}/%/'/**/*.plug(Nr)'}; do
        antibody bundle < $plug > $plug.bundle
        print -u2 "Created: $plug.bundle"
    done
}

function compile() {
    for dir file in ${(kv)files}; do
        chmod +x $dir/**/*.zshx(Nr^x) 2>/dev/null
        awk $awk_program $dir/**/*.(zsh|zshx|bundle)(Nr) > $file
        zcompile -UR $file
        print -u2 "Created: $file"
    done
}

function update() {
    antibody update >&2
    for script in ${~${(k)files}/%/'/**/update-script(Nr)'}; do
        print -u2 "Sourcing: $script"
        source $script
    done
}

(($# == 0)) && echo $help_message
for command; do
    case $command in
        clean)    clean  ;;
        bundle)   bundle ;;
        compile)  compile ;;
        update)   update ;;
        *)        echo $help_message ;;
    esac
done
